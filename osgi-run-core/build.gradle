buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.7

group = 'com.athaydes.gradle.osgi'
version = '1.4.0'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile group: 'biz.aQute.bnd', name: 'biz.aQute.bndlib', version: '3.1.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

/* Publishing config */

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    publish = true
    pkg {
        repo = 'maven'
        name = 'osgi-run-core'
        licenses = ['Apache-2.0']
        desc = 'A Gradle plugin to make the development of modular applications using OSGi completely painless'
        vcsUrl = 'https://github.com/renatoathaydes/osgi-run.git'
        websiteUrl = 'https://github.com/renatoathaydes/osgi-run'
        issueTrackerUrl = 'https://github.com/renatoathaydes/osgi-run/issues'
        labels = ['osgi', 'ipojo', 'modular']
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/renatoathaydes/osgi-run'
    vcsUrl = 'https://github.com/renatoathaydes/osgi-run'

    //noinspection GroovyAssignabilityCheck
    plugins {
        osgiRunPlugin {
            id = 'com.athaydes.osgi-run'
            displayName = 'Osgi-run Gradle Plugin'
            description = 'A Gradle plugin to make the development of modular applications using OSGi completely painless'
            tags = ['osgi', 'ipojo', 'modular']
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar
publishPlugins.dependsOn build, sourcesJar
